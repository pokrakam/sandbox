REPORT zsearch_cds.

PARAMETERS p_search type string default `transactionalProcessingEnabled`.

CLASS lcl_main DEFINITION.

  PUBLIC SECTION.
    METHODS start.
  PROTECTED SECTION.
  PRIVATE SECTION.
    METHODS get_cds_defs RETURNING VALUE(r_result) TYPE prognames.
    METHODS is_bopf IMPORTING i_ddlname       TYPE ddlname
                    RETURNING VALUE(r_result) TYPE abap_bool.
    METHODS get_source IMPORTING i_ddlname     TYPE ddlname
                       RETURNING VALUE(result) TYPE ddddlsource .

ENDCLASS.

CLASS lcl_main IMPLEMENTATION.

  METHOD start.

    DATA(cdstab) = get_cds_defs( ).
    LOOP AT cdstab INTO DATA(ddlname).
      IF is_bopf( ddlname ).
        WRITE / ddlname.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.

  METHOD get_cds_defs.
    SELECT ddlname FROM rsodpabapcdsview
           INTO TABLE @r_result.
  ENDMETHOD.

  METHOD is_bopf.

    IF find( val  = get_source( i_ddlname )
             sub  = p_search
             case = abap_false
           ) > 0.
      r_result = abap_true.
    ENDIF.

  ENDMETHOD.

  METHOD get_source.

    TRY.
        cl_dd_ddl_handler_factory=>create( )->read(
          EXPORTING
            name         = i_ddlname
          IMPORTING
            ddddlsrcv_wa = DATA(ddl_src)    ).

      CATCH cx_dd_ddl_read INTO DATA(fail).
        MESSAGE fail->get_text( ) TYPE 'E'.
    ENDTRY.

    result = ddl_src-source.

  ENDMETHOD.

ENDCLASS.

START-OF-SELECTION.
  new lcl_main( )->start( ).
